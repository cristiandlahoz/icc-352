plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.google.protobuf' version '0.9.4'
    id 'idea'

}

group 'edu.pucmm.eict'
version '1.0-SNAPSHOT'
description = 'serving-protobuf-with-javalin'

ext {
    openapi = "6.1.3"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

mainClassName = 'edu.pucmm.eict.ormjpa.Main'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    //dependencia de Javalin
    implementation 'io.javalin:javalin:6.1.3'

    //manejo de log.
    implementation "org.slf4j:slf4j-simple:2.0.11"

    // En la versión 5.3.X separaron las clases del renderizado en otro proyecto
    implementation "io.javalin:javalin-rendering:6.1.3"

    //Procesamiento JSON para Javalin.
    implementation("com.fasterxml.jackson.core:jackson-core:2.15.2")
    implementation('com.fasterxml.jackson.core:jackson-databind:2.15.2')
    implementation("com.fasterxml.jackson.core:jackson-annotations:2.15.2")

    //ORM - Hibernate.
    implementation 'org.hibernate.orm:hibernate-core:6.0.2.Final'
    //implementation 'org.hibernate:hibernate-entitymanager:6.0.0.Alpha7'

    //Driver de la conexión en h2...
    implementation 'com.h2database:h2:2.1.214'
    implementation 'org.postgresql:postgresql:42.3.8'

    //sistemas de plantilla:
    implementation 'org.thymeleaf:thymeleaf:3.1.1.RELEASE'

    //OpenApi
    annotationProcessor("io.javalin.community.openapi:openapi-annotation-processor:$openapi")
    implementation("io.javalin.community.openapi:javalin-openapi-plugin:$openapi") // for /openapi route with JSON scheme
    implementation("io.javalin.community.openapi:javalin-swagger-plugin:$openapi") // for Swagger UI
    implementation("io.javalin.community.openapi:javalin-redoc-plugin:$openapi") // for ReDoc UI

    //NUEVAS


    // gRPC core dependencies
    implementation 'io.grpc:grpc-netty-shaded:1.63.0'
    implementation 'io.grpc:grpc-protobuf:1.63.0'
    implementation 'io.grpc:grpc-stub:1.63.0'

    // (Opcional) Solo si usas Java 9+
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
}

shadowJar {
    archiveBaseName.set("app")
    archiveClassifier.set('')
    archiveVersion.set('')
}

task stage {
    dependsOn shadowJar
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.62.2'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

//Tareas para integrar con el
clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        // proto files and generated Java files are automatically added as
        // source dirs.
        // If you have additional sources, add them here:
        sourceDirs += file("src/generated");
    }
}

//Permitir tener acceso a la entrada estandar por Gradle
run{
    standardInput = System.in
}
